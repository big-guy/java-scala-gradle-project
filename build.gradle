plugins {
    id "io.spring.dependency-management" version "1.0.4.RELEASE" apply false // https://github.com/spring-gradle-plugins/dependency-management-plugin
    id "net.rdrei.android.buildtimetracker" version "0.11.0" apply false // https://github.com/passy/build-time-tracker-plugin
    id 'com.github.johnrengelman.shadow' version '4.0.3' apply false // https://github.com/johnrengelman/shadow
    id "com.palantir.git-version" version "0.10.0" apply false // https://github.com/palantir/gradle-git-version
    id "com.jfrog.artifactory" version "4.8.1" apply false
    id "org.scoverage" version "2.5.0" apply false // https://github.com/scoverage/gradle-scoverage
    id "net.saliman.cobertura" version "2.6.0" apply false // https://github.com/stevesaliman/gradle-cobertura-plugin
}

wrapper {
    gradleVersion = '5.0'
}

allprojects {
    apply from: new File(rootProject.projectDir, 'gradle/base.gradle')
}

configure(this) {
    applyFile("buildtimes")

    applyFile("scoverage")
    // this line must be here (and not in `scoverage.gradle`) due to classpath problems with the scoverage plugin
    task aggregateScoverage(type: org.scoverage.ScoverageAggregate)
    checkScoverage {
        reportDir = file("$buildDir/scoverage-aggregate")
        minimumRate = 0.75
    }

    applyFile("cobertura")

    gradle.projectsEvaluated {
        def testCoverageTask = tasks.create(name: "testCoverage")
        testCoverageTask.dependsOn(project.tasks.cobertura)
        def reportScoverageTasks = subprojects.collect { p -> p.tasks.matching { t -> t.name == "reportScoverage" } }
        aggregateScoverage.dependsOn(reportScoverageTasks)
        testCoverageTask.dependsOn(aggregateScoverage)
    }
}

subprojects {
    applyFile("java")
    applyFile("dependencies")
    applyFile("test")
}