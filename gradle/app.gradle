import java.time.LocalDate
import java.time.ZoneId
import java.time.format.DateTimeFormatter

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "com.palantir.git-version"

ext.configureApp = { archiveBaseName, mainClass ->

    mainClassName = mainClass

    shadowJar {
        archiveName = "${archiveBaseName}.jar"

        def buildNumber = {
            if (System.env.containsKey("BUILD_NUMBER")) System.env.get("BUILD_NUMBER") else "unspecified"
        }()

        def buildDate = DateTimeFormatter.ofPattern("dd MMM uuuu").format(LocalDate.now(ZoneId.of("UTC")))

        def customManifestSection = "Build-Details"

        manifest {
            attributes "Specification-Version": project.version.toString()
            attributes "Implementation-Version": buildNumber

            attributes "Implementation-Date": buildDate, customManifestSection
            attributes "Implementation-Revision": versionDetails().gitHash, customManifestSection
        }
    }

    shadowDistZip {
        baseName = archiveBaseName
    }
    shadowDistTar {
        baseName = archiveBaseName
    }

    def distTask = tasks.create(name: 'shadowDist')
    distTask.dependsOn(shadowDistZip)
    distTask.dependsOn(shadowDistTar)
}