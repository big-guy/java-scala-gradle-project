// scoverage only works with setting the environment variable:
// `JAVA_TOOL_OPTIONS=-Dfile.encoding=UTF-8`
// otherwise "reportScoverage" might fail due to a bug in scoverage which causes problems with encoding
// See https://github.com/scoverage/gradle-scoverage/issues/68

apply plugin: "org.scoverage"

applyFile("utils")

dependencies {
    scoverage group: 'org.scoverage', name: 'scalac-scoverage-plugin_2.12', version: '1.3.1'
    scoverage group: 'org.scoverage', name: 'scalac-scoverage-runtime_2.12', version: '1.3.1'
}

if (project != rootProject) {
    project.afterEvaluate {
        scoverage {
            excludedFiles = ["*\\.java"]
        }

        testScoverage {
            enabled = false
        }

        // delete all java classes after the instrumentation by scoverage (since they are not really instrumented)
        task deleteNonScoverageInstrumentedClasses {

            doLast {
                def classesDir = sourceSets.main.scala.outputDir
                def instrumentionDir = sourceSets.scoverage.scala.outputDir
                deleteSameFiles(classesDir, instrumentionDir)
            }
        }
        deleteNonScoverageInstrumentedClasses.dependsOn(compileScoverageScala)
        reportScoverage.dependsOn(deleteNonScoverageInstrumentedClasses)
        reportScoverage.dependsOn(test)
        test.mustRunAfter(deleteNonScoverageInstrumentedClasses)

        // we only want to alter 'test' task classpath if scoverage is requested to be executed
        gradle.taskGraph.whenReady { graph ->
            if (graph.hasTask(reportScoverage)) {
                test {
                    outputs.upToDateWhen { false }
                    classpath = testScoverage.classpath + test.classpath
                }
            }
        }
    }
}